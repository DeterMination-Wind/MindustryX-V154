plugins {
    id "com.badlogicgames.jnigen.jnigen-gradle"
}

file("jni").mkdir()

jnigen{
    sharedLibName = "arc-filedialogs"
    temporaryDir = file("build/target/native").absolutePath
    libsDir = file("libs").absolutePath

    all{
        //headerDirs += ["$fdir/"]
        cIncludes = ["tinyfiledialogs.c"]
    }
    addWindows(x32, x86){
        libraries = " -lcomdlg32 -lole32"
    }
    addWindows(x64, x86){
        libraries = " -lcomdlg32 -lole32"
    }
    addLinux(x64, x86)
    addMac(x64, x86)
    addMac(x64, ARM)
}

task preJni{

    doFirst{
        copy{
            from "$rootDir/arc-core/build/classes/java/main"
            into "$rootDir/extensions/filedialogs/build/classes/java/main"
            include "**"
        }

        copy{
            from "csrc/"
            into "jni/"
            include "**"
        }
    }
}

//copies files into the right locations
task postJni{
    doLast{
        copy{
            from "libs/linux64", "libs/windows32", "libs/windows64", "libs/macosx64"
            into "../../natives/natives-filedialogs/libs"
            include "**"
        }
    }
}

preJni.dependsOn "compileJava"
jnigenBuildHost.finalizedBy postJni

getTasksByName("jnigen", true).each{
    it.dependsOn preJni
}
