plugins{
    id("com.android.application") version "8.9.2"
}

configurations{ natives }

repositories{
    mavenCentral()
    maven{ url = "https://dl.google.com/dl/android/maven2/" }
}

android{
    namespace = "com.github.tinylake.mindustryX"
    compileSdk = 36
    sourceSets{
        main{
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['../core/assets/', '../../assets/']
            jniLibs.srcDirs = ['libs']
        }

        androidTest.setRoot('tests')
    }

    packagingOptions{
        exclude 'META-INF/robovm/ios/robovm.xml'
    }

    defaultConfig{
        def versionNameResult = "$versionNumber-$versionType-${getBuildVersion().replace(" ", "-")}"

        applicationId "com.github.tinylake.mindustryX"
        minSdkVersion 21
        targetSdkVersion 36
        versionName versionNameResult
        versionCode = 1
        multiDexEnabled = true

        if(project.hasProperty("release")){
            Properties props = loadVersionProps()
            Integer vcode = props['androidBuildCode']?.toInteger() ?: 1
            versionCode = vcode
            props['androidBuildCode'] = (vcode + 1).toString()
            props.store(file('../core/assets/version.properties').newWriter(), null)
        }
    }

    compileOptions{
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    flavorDimensions = ["google"]

    signingConfigs{
        release{
            if(project.hasProperty("RELEASE_STORE_FILE")){
                storeFile = file(RELEASE_STORE_FILE)
                storePassword = RELEASE_STORE_PASSWORD
                keyAlias = RELEASE_KEY_ALIAS
                keyPassword = RELEASE_KEY_PASSWORD
            }else if(System.getenv("CI") == "true"){
                storeFile = file("../../bekeystore.jks")
                storePassword = System.getenv("keystore_password")
                keyAlias = System.getenv("keystore_alias")
                keyPassword = System.getenv("keystore_alias_password")
            }else{
                println("No keystore property found. Releases will be unsigned.")
            }
        }
    }

    buildTypes{
        release{
            //TODO without these lines (r8 enabled), Mindustry crashes with missing default interface method errors.
            //WHY THE HELL ARE DEFAULT INTERFACES NOT BEING DESUGARED? WHY DID UPDATING AGP MAKE THIS HAPPEN?
            //When I ENABLE shrinking, r8 goes and REMOVES ALL DEFAULT INTERFACE CLASSES, which breaks mods. Why?
            //-keep class mindustry.** { *; } should *keep the classes* - WHY IS R8 REMOVING THEM?
            minifyEnabled = true
            shrinkResources = true
            proguardFiles("proguard-rules.pro")
            if(project.hasProperty("RELEASE_STORE_FILE") || System.getenv("CI") == "true"){
                signingConfig = signingConfigs.release
            }
        }
    }
}

dependencies{
    implementation project(":core")

    implementation arcModule("backends:backend-android")
    implementation 'com.jakewharton.android.repackaged:dalvik-dx:9.0.0_r3'
    implementation 'androidx.core:core:1.16.0'

    natives arcModule("natives-android")
    natives arcModule("natives-freetype-android")
}

tasks.register('copyAndroidNatives', Copy){
    dependsOn configurations.natives
    from configurations.natives.files.collect{ zipTree(it) }
    exclude("META-INF/**")
    into file("libs/")
}

tasks.register('run', Exec){
    commandLine "adb", 'shell', 'am', 'start', '-n', 'io.anuke.mindustry/mindustry.android.AndroidLauncher'
}

tasks.configureEach{ task ->
    if(task.name == 'mergeDebugAssets' || task.name == 'mergeReleaseAssets' || task.name == 'lintVitalAnalyzeRelease' || task.name == 'generateReleaseLintVitalReportModel'){
        task.dependsOn ":core:allAssets"
    }
    if(task.name == 'mergeDebugJniLibFolders' || task.name == 'mergeReleaseJniLibFolders'){
        task.dependsOn "copyAndroidNatives"
    }
    if(task.name == 'mergeReleaseAssets' || task.name == 'compressReleaseAssets'){
        task.outputs.doNotCacheIf("Not worth caching"){ true }
    }
}