buildscript{
    ext{
        arcHash = findProperty("archash") ?: "v${property("upstreamBuild")}"

        localArc = !project.hasProperty("release") && new File(rootDir.parent, 'Arc').exists() && !project.hasProperty("noLocalArc")

        arcModule = { String name ->
            def useLocalArc = localArc
            //skip to last submodule
            name = name.substring(name.lastIndexOf(':') + 1)
            return "com.github.Anuken${useLocalArc ? "" : ".Arc"}:$name:$arcHash"
        }
    }
}

plugins{
    id "org.jetbrains.kotlin.jvm" version "2.1.10"
    id "org.jetbrains.kotlin.kapt" version "2.1.10"
}

allprojects{
    apply plugin: 'maven-publish'

    version = project.hasProperty("packageVersion") ? project.getProperty("packageVersion") : 'release'
    group = 'cf.wayzer.MindustryX'

    ext{
        versionNumber = '8'
        if(!project.hasProperty("versionModifier")) versionModifier = 'beta'
        if(!project.hasProperty("versionType")) versionType = 'official'
        appName = 'Mindustry'
        steamworksVersion = '0b86023401880bb5e586bc404bedbaae9b1f1c94'
        rhinoVersion = '54b75cbd1207ef1fd5cff517cbd188e763c85baa'

        loadVersionProps = {
            return new Properties().with{p -> p.load(file('../core/assets/version.properties').newReader()); return p }
        }

        getBuildVersion = {
            if(!project.hasProperty("buildversion")) return "custom build"
            return project.getProperties()["buildversion"]
        }
    }

    repositories{
        mavenLocal()
        mavenCentral()
        maven{ url = "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven{ url = "https://oss.sonatype.org/content/repositories/releases/" }
        maven{ url = 'https://jitpack.io' }
    }

    tasks.withType(JavaCompile).configureEach{
        if(project.name == "annotations"){
            sourceCompatibility = 8
            targetCompatibility = 8
            options.setFork(true)
        }else{
            sourceCompatibility = JavaVersion.VERSION_17
            targetCompatibility = JavaVersion.VERSION_17
            if(project.name != "android" && project.name != "tools"){
                targetCompatibility = 8
                options.release = 8
            }
            if(project.name == "core")
                options.setFork(true)
        }
        options.encoding = "UTF-8"
        options.compilerArgs += ["-Xlint:deprecation"]
    }
}

//compile with java 8 compatibility for everything except the annotation project
configure(subprojects - project(":annotations") - project(":tests")){
    tasks.withType(Javadoc).configureEach{
        options{
            addStringOption('Xdoclint:none', '-quiet')
            addStringOption('-release', '17')
            encoding('UTF-8')
        }
    }
}

if(findProject("desktop") != null)
project(":desktop"){
    apply plugin: "java"

    dependencies{
        implementation project(":core")
        implementation arcModule("extensions:profiling")
        implementation arcModule("extensions:discord")
        implementation arcModule("natives:natives-filedialogs")
        implementation arcModule("natives:natives-desktop")
        implementation arcModule("natives:natives-freetype-desktop")

        implementation "com.github.Anuken:steamworks4j:$steamworksVersion"

        implementation arcModule("backends:backend-sdl")
        annotationProcessor 'com.github.Anuken:jabel:0.9.0'
    }
}

project(":core"){
    apply plugin: "java-library"
    apply plugin: "kotlin"
    apply plugin: "kotlin-kapt"

    kapt{
        javacOptions{
            option("-source", "17")
            option("-target", "1.8")
        }
    }

    tasks.register('copyChangelog'){
        doLast{
            def props = loadVersionProps()
            def androidVersion = props['androidBuildCode'].toInteger() - 2
            def loglines = file("../changelog").text.split("\n")
            def notice = "[This is a truncated changelog, see Github for full notes]"
            def maxLength = 460

            def androidLogList = [notice] + loglines.findAll{ line -> !line.endsWith("]") || line.endsWith("[Mobile]") || line.endsWith("[Android]") }
            def result = ""
            androidLogList.forEach{ line ->
                if(result.length() + line.length() + 1 < maxLength){
                    result += line + "\n"
                }
            }
            def changelogs = file("../fastlane/metadata/android/en-US/changelogs/")
            changelogs.mkdirs()
            try{
                new File(changelogs, androidVersion + ".txt").text = (result)
            }catch(Exception ignored){
            }
        }
    }

    tasks.register('musicJar', Jar){
        archiveClassifier = 'music'
        from files("assets"){
            include "music/*", "sounds/*"
        }
    }

    dependencies{
        api "org.lz4:lz4-java:1.8.0"
        api arcModule("arc-core")
        api arcModule("extensions:flabel")
        api arcModule("extensions:freetype")
        api arcModule("extensions:g3d")
        api arcModule("extensions:fx")
        api arcModule("extensions:arcnet")
        implementation arcModule("extensions:filedialogs")
        api "com.github.Anuken:rhino:$rhinoVersion"

//        compileOnly 'com.github.bsideup.jabel:jabel-javac-plugin:0.4.2'
        annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:0.4.2'
//        annotationProcessor 'com.github.Anuken:jabel:0.9.0'
        compileOnly project(":annotations")
        if(!project.hasProperty("noKapt")) kapt project(":annotations")
    }

    tasks.register("mergedJavadoc", Javadoc) {
        // 黑名单依然可以是配置阶段的常量，因为它是静态数据
        def excludedPaths = [':server', ':annotations', ':ios', ':desktop', ':android'] as Set

        def getJavaProjects = {
            rootProject.subprojects.findAll { project ->
                !excludedPaths.contains(project.path) && project.plugins.hasPlugin("java")
            }
        }
        source = files {
            getJavaProjects().collect { it.sourceSets.main.allJava }
        }
        classpath = files {
            getJavaProjects().collect { it.sourceSets.main.compileClasspath }
        }
        destinationDir = layout.buildDirectory.dir("javadoc").get().asFile
    }

    //comp** classes are only used for code generation
    jar{
        exclude("mindustry/entities/comp/**")
    }
}

project(":server"){
    apply plugin: "java"

    dependencies{
        implementation project(":core")
        implementation arcModule("backends:backend-headless")
        annotationProcessor 'com.github.Anuken:jabel:0.9.0'
    }
}

project(":tests"){
    apply plugin: "java"

    dependencies{
        testImplementation project(":core")
        testImplementation "org.junit.jupiter:junit-jupiter-params:5.7.1"
        testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.1"
        testImplementation arcModule("backends:backend-headless")
        testImplementation "org.json:json:20230618"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.1"
    }

    test{
        //fork every test so mods don't interact with each other
        forkEvery = 1
        useJUnitPlatform()
        workingDir = new File("../core/assets")
        testLogging{
            exceptionFormat = 'full'
            showStandardStreams = true
        }
    }
}

project(":tools"){
    apply plugin: "java"

    dependencies{
        //MDTX: use upstream build for tools, avoid classpath change
        implementation("com.github.Anuken.Mindustry:core:v${property("upstreamBuild")}"){
            transitive = false
        }
        implementation arcModule("arc-core")
        implementation arcModule(":extensions:g3d")
        implementation arcModule(":extensions:packer")

        implementation arcModule("natives:natives-desktop")
        implementation arcModule("natives:natives-freetype-desktop")
        implementation arcModule("backends:backend-headless")

        implementation("com.google.guava:guava:33.3.1-jre")
    }
}

project(":annotations"){
    apply plugin: "java-library"

    dependencies{
        implementation 'com.squareup:javapoet:1.12.1'
        implementation arcModule("arc-core")
    }
}

configure([":core", ":server"].collect{project(it)}){
    java{
        withJavadocJar()
        withSourcesJar()
    }

    publishing{
        publications{
            maven(MavenPublication){
                from components.java
            }
        }
    }
}

tasks.register('resolveDependencies'){
    doLast{
        rootProject.allprojects{ project ->
            Set<Configuration> configurations = project.buildscript.configurations + project.configurations
            configurations.findAll{ c -> c.canBeResolved }.forEach{ c -> c.resolve() }
        }
    }
}
